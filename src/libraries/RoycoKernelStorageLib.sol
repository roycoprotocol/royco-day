// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.28;

import { BASE_UNIT, TRANCHE_UNIT } from "./Units.sol";

/**
 * @notice Initialization parameters for the Royco Kernel
 * @custom:field initialAuthority - The access manager for this kernel
 * @custom:field accountant - The address of the Royco accountant used to perform per operation accounting for this kernel
 * @custom:field protocolFeeRecipient - The market's protocol fee recipient
 */
struct RoycoKernelInitParams {
    address initialAuthority;
    address accountant;
    address protocolFeeRecipient;
}

/**
 * @notice Storage state for the Royco Kernel
 * @custom:storage-location erc7201:Royco.storage.RoycoKernelState
 * @custom:field protocolFeeRecipient - The market's configured protocol fee recipient
 * @custom:field accountant - The address of the Royco accountant used to perform per operation accounting for this kernel
 * @custom:field stOwnedYieldBearingAssets - The yield bearing assets held by the ST
 * @custom:field jtOwnedYieldBearingAssets - The yield bearing assets held by the ST
 * @custom:field stLiquidationProceeds - Accumulated liquidation proceeds for the senior tranche, in base asset units
 */
struct RoycoKernelState {
    address protocolFeeRecipient;
    address accountant;
    TRANCHE_UNIT stOwnedYieldBearingAssets;
    TRANCHE_UNIT jtOwnedYieldBearingAssets;
    BASE_UNIT stLiquidationProceeds;
}

/// @title RoycoKernelStorageLib
/// @notice Library for managing Royco Kernel storage using the ERC7201 pattern
library RoycoKernelStorageLib {
    /// @dev Storage slot for RoycoKernelState using ERC-7201 pattern
    // keccak256(abi.encode(uint256(keccak256("Royco.storage.RoycoKernelState")) - 1)) & ~bytes32(uint256(0xff))
    bytes32 private constant BASE_KERNEL_STORAGE_SLOT = 0xf8fc0d016168fef0a165a086b5a5dc3ffa533689ceaf1369717758ae5224c600;

    /// @notice Initializes the Royco kernel state
    /// @param _params The initialization parameters for the kernel
    function __RoycoKernel_init(RoycoKernelInitParams memory _params) internal {
        // Set the initial state of the kernel
        RoycoKernelState storage $ = _getRoycoKernelStorage();
        $.protocolFeeRecipient = _params.protocolFeeRecipient;
        $.accountant = _params.accountant;
    }

    /**
     * @notice Returns a storage pointer to the RoycoKernelState storage
     * @dev Uses ERC-7201 storage slot pattern for collision-resistant storage
     * @return $ Storage pointer to the kernel's state
     */
    function _getRoycoKernelStorage() internal pure returns (RoycoKernelState storage $) {
        assembly ("memory-safe") {
            $.slot := BASE_KERNEL_STORAGE_SLOT
        }
    }
}
